cmake_minimum_required(VERSION 3.14)

project(cxxui VERSION 0.1.0 LANGUAGES CXX)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
if(NOT DEFINED CMAKE_CXX_STANDARD)
    set(CMAKE_CXX_STANDARD 17)
endif()

if(CMAKE_PROJECT_NAME STREQUAL PROJECT_NAME)
    set(IS_TOPLEVEL_PROJECT TRUE)
else()
    set(IS_TOPLEVEL_PROJECT FALSE)
endif()
option(CXXUI_BUILD_EXAMPLES "Build examples" ${IS_TOPLEVEL_PROJECT})
option(CXXUI_USE_WEB_WINDOW "Use WebWindow" ${IS_TOPLEVEL_PROJECT})
if(CXXUI_USE_WEB_WINDOW)
    option(CXXUI_USE_BUILTIN_WEBVIEW "Use built-in WebView Library" ON)
    option(CXXUI_USE_BUILTIN_JSON "Use built-in JSON(nlohmann) Library" ON)
endif()

add_library(${PROJECT_NAME} INTERFACE)
target_include_directories(${PROJECT_NAME} INTERFACE ${PROJECT_SOURCE_DIR}/include)
if(WIN32)
    target_link_libraries(${PROJECT_NAME} INTERFACE user32 dwmapi shcore shlwapi)
    if(NOT MSVC AND NOT CMAKE_BUILD_TYPE STREQUAL "Debug")
        target_link_options(${PROJECT_NAME} INTERFACE -mwindows)
    endif()
endif()


if(CXXUI_BUILD_EXAMPLES)
    add_subdirectory(examples)
endif()
if(CXXUI_USE_BUILTIN_WEBVIEW)
    include(cmake/webview.cmake)
endif()
if(CXXUI_USE_BUILTIN_JSON)
    include(cmake/nlohmann.cmake)
endif()
